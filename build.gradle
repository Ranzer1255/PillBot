plugins {
    id 'java'
    id 'org.ajoberstar.grgit' version '5.0.0'
    id "org.ajoberstar.reckon" version "0.16.1"
}

group 'ranzer'

repositories {
    mavenCentral()
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
}

dependencies {

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api',    version: '5.9.2'
    testRuntimeOnly    group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.2'

    implementation group: 'net.dv8tion',         name: 'JDA',           version: '5.0.0-alpha.11'
    implementation group: 'org.apache.commons', name: 'commons-text',   version: '1.10.0'
    implementation group: 'org.jsoup',          name: 'jsoup',          version: '1.15.3'
    implementation group: 'org.json',           name: 'json',           version: '20230227'
    implementation group: 'org.slf4j',          name: 'slf4j-simple',   version: '2.0.5'

}

reckon{
    stages('dev','final')


    scopeCalc = calcScopeFromProp().or(calcScopeFromCommitMessages()) // fall back to commit message (see below) if no explicit prop set
    stageCalc = calcStageFromProp()

    defaultInferredScope = 'patch'
}

tasks.named('jar'){
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes (
                "Main-Class": "net.ranzer.grimco.pillbot.PillBot",
                "version"   : project.version
        )
    }

    from {
        configurations.runtimeClasspath.collect{it.isDirectory() ? it : zipTree(it)}
    }
}

test {
    useJUnitPlatform()
}
